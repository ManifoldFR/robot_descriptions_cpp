ADD_PROJECT_DEPENDENCY(pinocchio REQUIRED)
ADD_PROJECT_DEPENDENCY(example-robot-data REQUIRED)

set(ASSET_SRC_DIR ${PROJECT_SOURCE_DIR}/assets)
set(ASSET_INSTALL_DIR ${CMAKE_INSTALL_FULL_DATAROOTDIR}/${PROJECT_NAME})
set(PROJECT_USE_CMAKE_EXPORT True)

file(GLOB robot_descriptions_SOURCES CONFIGURE_DEPENDS ${PROJECT_NAME}/*.cpp)

add_library(${PROJECT_NAME} SHARED ${robot_descriptions_SOURCES})
target_include_directories(
  ${PROJECT_NAME}
  PUBLIC
    $<INSTALL_INTERFACE:include>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
)

target_link_libraries(
  ${PROJECT_NAME}
  PUBLIC example-robot-data::example-robot-data pinocchio::pinocchio_parsers
  PRIVATE toml11
)

target_compile_definitions(
  ${PROJECT_NAME}
  PUBLIC
    $<BUILD_INTERFACE:ROBOT_DESCRIPTIONS_ASSET_DIR="${ASSET_SRC_DIR}">
    $<INSTALL_INTERFACE:ROBOT_DESCRIPTIONS_ASSET_DIR="${ASSET_INSTALL_DIR}">
)

install(
  TARGETS ${PROJECT_NAME}
  EXPORT ${TARGETS_EXPORT_NAME}
  INCLUDES DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_FULL_LIBDIR}
)

install(
  DIRECTORY ${ASSET_SRC_DIR}
  DESTINATION ${ASSET_INSTALL_DIR}
  FILES_MATCHING
  PATTERN "*.toml"
)

file(GLOB robot_descriptions_HEADERS CONFIGURE_DEPENDS ${PROJECT_NAME}/*.hpp)

install(
  FILES ${robot_descriptions_HEADERS}
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}
  PERMISSIONS OWNER_READ GROUP_READ WORLD_READ OWNER_WRITE
)
